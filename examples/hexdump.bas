100 REM PRINT HEX MEMORY DUMP 
109 REM INITIALIZE 
110 A=-10 
120 B=-11 
130 C=-12 
140 D=-13 
150 E=-14 
160 F=-15 
170 X = -1 
175 O = 0 
180 LET S = 256 
190 REMARK: S IS BEGINNING OF TINY (IN DECIMAL) 
200 REM GET (HEX) ADDRESSES 
210 PRINT "DUMP: L,U"; 
215 REM INPUT STARTING ADDRESS IN HEX 
220 GOSUB 500 
230 L=N 
235 REM INPUT ENDING ADDRESS IN HEX 
240 GOSUB 500 
250 U=N 
275 REM TYPE OUT ADDRESS 
280 GOSUB 450 
290 REM GET MEMORY BYTE 
300 REM LET N = USR (S+20,L) 
305 REM CONVERT IT TO HEX 
310 LET M = N/16 
320 LET N = N-M*16 
330 PRINT " "; 
335 REM PRINT IT 
340 GOSUB 400+M+M 
350 GOSUB 400+N+N 
355 REM END? 
360 IF L=U GO TO 390 
365 L=L+1 
370 IF L/16*16 = L GOTO 280 
375 REM DO 16 BYTES PER LINE 
380 GO TO 300 
390 PRINT 
395 END 
399 REM PRINT ONE HEX DIGIT 
400 PRINT O; 
401 RETURN 
402 PRINT 1; 
403 RETURN 
404 PRINT 2; 
405 RETURN 
406 PRINT 3; 
407 RETURN 
408 PRINT 4; 
409 RETURN 
410 PRINT 5; 
411 RETURN 
412 PRINT 6; 
413 RETURN 
414 PRINT 7; 
415 RETURN 
416 PRINT 8; 
417 RETURN 
418 PRINT 9; 
419 RETURN 
420 PRINT "A"; 
421 RETURN 
422 PRINT "B"; 
423 RETURN 
424 PRINT "C"; 
425 RETURN 
426 PRINT "D"; 
427 RETURN 
428 PRINT "E"; 
429 RETURN 
430 PRINT "F"; 
431 RETURN 
440 REM PRINT HEX ADDRESS 
450 PRINT 
455 REM CONVERT IT TO HEX 
460 N = L/4096 
470 IF L<0 N=(L-32768)/4096+8 
480 GOSUB 400+N+N 
483 LET N=(L-N*4096) 
486 GOSUB 400+N/256*2 
490 GOSUB 400+(N-N/256*256)/16*2 
495 GOTO 400+(N-N/16*16)*2 
496 REM GOTO=GOSUB,RETURN 
500 REM INPUT HEX NUMBER 
501 REM FORMAT IS NNNNX 
502 REM WHERE "N" IS ANY HEX DIGIT 
505 N=0 
509 REM INPUT LETTER OR STRING OF DIGITS 
510 INPUT R 
520 IF R=X RETURN 
525 REM CHECK FOR ERROR 
530 IF R>9999 THEN PRINT "BAD HEX ADDRESS"
531 REM NOTE ERROR STOP ON LINE 530 (ON PURPOSE!) 
535 REM CONVERT INPUT DECIMAL DIGITS TO HEX 
540 IF R>999 THEN N=N*16 
545 IF R>99 THEN N=N*16 
550 IF R>9 THEN N=N*16 
555 IF R>0 THEN R=R+R/1000*1536+R/100*96+R/10*6 
559 REM PICK UP NON-DECIMAL DIGIT LETTERS 
560 IF R<0 THEN LET R=-R 
565 REM ADD NEW DIGIT TO PREVIOUS NUMBER 
570 LET N=N*16+R 
580 GOTO 510 
